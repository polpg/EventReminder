apply plugin: 'com.android.application'


ext.versionMajor = 0
ext.versionMinor = 2
ext.versionPatch = 1
ext.versionClassifier = null
ext.isSnapshot = false
ext.minimumSdkVersion = 16

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cat.polpadilla.eventreminder"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        testApplicationId "cat.polpadilla.eventreminder.test"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    def appName = "Event Reminder"
                    outputFileName = appName+"-${variant.versionName}.apk"
                }
            }
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

//https://stackoverflow.com/questions/46014619
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'androidx.room:room-runtime:2.1.0-alpha04'
    implementation 'androidx.room:room-rxjava2:2.1.0-alpha04'
    annotationProcessor 'androidx.room:room-compiler:2.1.0-alpha04'
    annotationProcessor "com.google.auto.value:auto-value:1.6.2"
    api "com.google.auto.value:auto-value-annotations:1.6.2"
    implementation 'me.everything:overscroll-decor-android:1.0.4'
    implementation 'com.github.prolificinteractive:material-calendarview:2.0.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
